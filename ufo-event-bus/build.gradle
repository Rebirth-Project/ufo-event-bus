/*
 * Copyright (C) 2021 Andrea Paternesi Rebirth project
 * Modifications copyright (C) 2021 Matteo Veroni Rebirth project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

plugins {
    id "signing"
    id "java-library"
    id 'maven-publish'
    id 'org.javamodularity.moduleplugin' version '1.8.12'   
}

group = 'it.rebirthproject'
archivesBaseName = 'ufoeventbus'

modularity.mixedJavaRelease 8

Date buildTimeAndDate = new Date()
ext {   
    buildDate = new SimpleDateFormat('dd-MM-yyyy').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

repositories {
    mavenCentral()
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

compileTestJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

dependencies {
    api 'org.slf4j:slf4j-api:2.0.2'
    testImplementation 'ch.qos.logback:logback-classic:1.4.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

tasks.withType(Jar) {
    manifest.attributes(
            'Specification-Title': 'UFO Event Bus',
            'Specification-Vendor': 'Rebirth Project',
            'Specification-Version': archiveVersion.get().replace('-SNAPSHOT', ''),
            'Implementation-Title': 'UFO Event Bus',
            'Implementation-Vendor': 'Rebirth Project',
            'Implementation-Version': archiveVersion.get(),
            'Build-Date': project.buildDate,
            'Build-Time': project.buildTime
    )
}

javadoc {
    failOnError = false
    title = "EventBus ${version} API"
    options.encoding = 'UTF-8'
    options.bottom = ''
    options.showFromPrivate()
    options.setWindowTitle("UfoEventBus JavaDoc")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test {
    //    debug = true
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

signing {
    //If we are in guthub then we will find those properties otherwise we are in local PC and we use directly gpg keyring (default)    
    if(project.hasProperty('ORG_GRADLE_PROJECT_signingKey') && project.hasProperty('ORG_GRADLE_PROJECT_signingPassword')) {
        println 'La variabile esiste?? -------------------> '+project.ORG_GRADLE_PROJECT_signingPassword
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign configurations.archives
    }        
    else {
        sign configurations.archives
    }
}

//uploadArchives {
//    repositories {
//        mavenDeployer {         
//            pom.project {
//                name 'EventBus'
//                description 'EventBus is a publish/subscribe event bus.'
//            }
//        }
//    }
//}

